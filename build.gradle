import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.12.2'
}

group 'net.simplyvanilla'
version '0.8.2'

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()

    maven {
        url 'https://repo.papermc.io/repository/maven-public/'
    }

    maven {
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'io.github.miniplaceholders:miniplaceholders-api:2.2.3'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.20:3.66.1'
    testImplementation 'org.awaitility:awaitility:4.2.0'
    testRuntimeOnly 'mysql:mysql-connector-java:8.0.33'
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.11' // specify the desired version
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

coveralls {
    jacocoReportPath 'build/reports/jacoco/test/jacocoTestReport.xml'
}

processResources {
    duplicatesStrategy = 'include'

    from(sourceSets.main.resources.srcDirs) {
        filter ReplaceTokens, tokens: [version: version]
    }
}
